namespace Hedgehog.Linq

#if !FABLE_COMPILER

open System
open System.Runtime.CompilerServices
open Hedgehog
open Hedgehog.FSharp

type Property = private Property of Property<unit> with

    static member Failure : Property =
        Property.failure |> Property

    static member Discard : Property =
        Property.discard |> Property

    static member Success (value : 'T) : Property<'T> =
        Property.success value

    static member FromBool (value : bool) : Property =
        value |> Property.ofBool |> Property

    static member FromGen (gen : Gen<Lazy<Journal * Outcome<'T>>>) : Property<'T> =
        Property.ofGen gen

    static member FromOutcome (result : Outcome<'T>) : Property<'T> =
        Property.ofOutcome result

    static member Delay (f : Func<Property<'T>>) : Property<'T> =
        Property.delay f.Invoke

    static member Using (resource : 'T, action : Func<'T, Property<'TResult>>) : Property<'TResult> =
        Property.using resource action.Invoke

    /// <summary>
    /// Adds custom debug information that will be displayed when a property test fails.
    /// Use this to include variable values or other context to help diagnose failures.
    /// </summary>
    /// <param name="message">A function that returns the debug message to display.</param>
    /// <returns>A property that includes the counterexample message.</returns>
    /// <example>
    /// <code>
    /// var property =
    ///     from x in Gen.Int32(Range.Constant(0, 1000)).ForAll()
    ///     from y in Gen.Int32(Range.Constant(-100, 1000)).ForAll()
    ///     from _ in Property.CounterExample(() => $"x = {x}, y = {y}, x * y = {x * y}")
    ///     select x * y >= 0;
    /// </code>
    /// </example>
    static member CounterExample (message : Func<string>) : Property<unit> =
        Property.counterexample message.Invoke

    [<Obsolete("Use .ForAll() extension method")>]
    static member ForAll (gen : Gen<'T>, k : Func<'T, Property<'TResult>>) : Property<'TResult> =
        Property.forAll k.Invoke gen

    [<Obsolete("Use .ForAll() extension method")>]
    static member ForAll (gen : Gen<'T>) : Property<'T> =
        Property.forAll' gen


[<AbstractClass; Sealed>]
type PropertyExtensions private () =

    /// <summary>
    /// Creates a property from a generator, allowing it to be used in LINQ query comprehensions.
    /// "For all" means the property will be tested for all values of that type generated by the generator.
    /// </summary>
    /// <param name="gen">The generator to create a property from.</param>
    /// <returns>A property that can be used in LINQ comprehensions.</returns>
    [<Extension>]
    static member ForAll(gen : Gen<'T>): Property<'T> =
        Property.forAll' gen

    /// <summary>
    /// Creates a property from a generator and applies a function to it, allowing it to be used in LINQ query comprehensions.
    /// "For all" means the property will be tested for all values of that type generated by the generator.
    /// </summary>
    /// <param name="gen">The generator to create a property from.</param>
    /// <param name="bind">The function to apply to each generated value.</param>
    /// <returns>A property that can be used in LINQ comprehensions.</returns>
    [<Extension>]
    static member ForAll(gen : Gen<'T>, bind : Func<'T, Property<'TResult>>): Property<'TResult> =
        Property.forAll bind.Invoke gen

    [<Extension>]
    static member ToGen (property : Property<'T>) : Gen<Lazy<Journal * Outcome<'T>>> =
        Property.toGen property

    [<Extension>]
    static member TryFinally (property : Property<'T>, onFinally : Action) : Property<'T> =
        Property.tryFinally onFinally.Invoke property

    [<Extension>]
    static member TryWith (property : Property<'T>, onError : Func<exn, Property<'T>>) : Property<'T> =
        Property.tryWith onError.Invoke property

    [<Extension>]
    static member Report (property : Property) : Report =
        let (Property property) = property
        Property.report property

    [<Extension>]
    static member Report (property : Property, config : IPropertyConfig) : Report =
        let (Property property) = property
        Property.reportWith config property

    [<Extension>]
    static member Report (property : Property<bool>) : Report =
        property |> Property.falseToFailure |> Property.report

    [<Extension>]
    static member Report (property : Property<bool>, config : IPropertyConfig) : Report =
        property |> Property.falseToFailure |> Property.reportWith config

    /// <summary>
    /// Runs the property test and throws an exception if it fails.
    /// Use this in unit tests to verify that a property holds for randomly generated test cases.
    /// </summary>
    /// <param name="property">The property to check.</param>
    [<Extension>]
    static member Check (property : Property) : unit =
        let (Property property) = property
        Property.check property

    /// <summary>
    /// Runs the property test with custom configuration and throws an exception if it fails.
    /// Use this in unit tests to verify that a property holds for randomly generated test cases.
    /// </summary>
    /// <param name="property">The property to check.</param>
    /// <param name="config">Custom property configuration (e.g., number of test cases).</param>
    [<Extension>]
    static member Check (property : Property, config : IPropertyConfig) : unit =
        let (Property property) = property
        Property.checkWith config property

    /// <summary>
    /// Runs the boolean property test and throws an exception if it fails.
    /// Use this in unit tests to verify that a property holds for randomly generated test cases.
    /// </summary>
    /// <param name="property">The boolean property to check.</param>
    [<Extension>]
    static member Check (property : Property<bool>) : unit =
        property |> Property.falseToFailure |> Property.check

    /// <summary>
    /// Runs the boolean property test with custom configuration and throws an exception if it fails.
    /// Use this in unit tests to verify that a property holds for randomly generated test cases.
    /// </summary>
    /// <param name="property">The boolean property to check.</param>
    /// <param name="config">Custom property configuration (e.g., number of test cases).</param>
    [<Extension>]
    static member Check (property : Property<bool>, config : IPropertyConfig) : unit =
        property |> Property.falseToFailure |> Property.checkWith config

    /// <summary>
    /// Rechecks a previously failed property test using the same random seed and test inputs.
    /// Use this to reproduce and debug a specific failure without running all test cases again.
    /// </summary>
    /// <param name="property">The property to recheck.</param>
    /// <param name="recheckData">The recheck data string from a previous test failure report.</param>
    [<Extension>]
    static member Recheck (property : Property, recheckData: string) : unit =
        let (Property property) = property
        Property.recheck recheckData property

    /// <summary>
    /// Rechecks a previously failed property test using the same random seed and test inputs, with custom configuration.
    /// Use this to reproduce and debug a specific failure without running all test cases again.
    /// </summary>
    /// <param name="property">The property to recheck.</param>
    /// <param name="recheckData">The recheck data string from a previous test failure report.</param>
    /// <param name="config">Custom property configuration.</param>
    [<Extension>]
    static member Recheck (property : Property, recheckData: string, config : IPropertyConfig) : unit =
        let (Property property) = property
        Property.recheckWith recheckData config property

    /// <summary>
    /// Rechecks a previously failed boolean property test using the same random seed and test inputs.
    /// Use this to reproduce and debug a specific failure without running all test cases again.
    /// </summary>
    /// <param name="property">The boolean property to recheck.</param>
    /// <param name="recheckData">The recheck data string from a previous test failure report.</param>
    [<Extension>]
    static member Recheck (property : Property<bool>, recheckData: string) : unit =
        property |> Property.falseToFailure |> Property.recheck recheckData

    /// <summary>
    /// Rechecks a previously failed boolean property test using the same random seed and test inputs, with custom configuration.
    /// Use this to reproduce and debug a specific failure without running all test cases again.
    /// </summary>
    /// <param name="property">The boolean property to recheck.</param>
    /// <param name="recheckData">The recheck data string from a previous test failure report.</param>
    /// <param name="config">Custom property configuration.</param>
    [<Extension>]
    static member Recheck (property : Property<bool>, recheckData: string, config : IPropertyConfig) : unit =
        property |> Property.falseToFailure |> Property.recheckWith recheckData config

    [<Extension>]
    static member ReportRecheck (property : Property, recheckData: string) : Report =
        let (Property property) = property
        Property.reportRecheck recheckData property

    [<Extension>]
    static member internal ReportRecheck (property : Property, recheckData: RecheckData) : Report =
        let (Property property) = property
        Property.reportRecheck (RecheckData.serialize recheckData) property

    [<Extension>]
    static member ReportRecheck (property : Property, recheckData: string, config : IPropertyConfig) : Report =
        let (Property property) = property
        Property.reportRecheckWith recheckData config property

    [<Extension>]
    static member ReportRecheck (property : Property<bool>, recheckData: string) : Report =
        property |> Property.falseToFailure |> Property.reportRecheck recheckData

    [<Extension>]
    static member ReportRecheck (property : Property<bool>, recheckData: RecheckData) : Report =
        property |> Property.falseToFailure |> Property.reportRecheck (RecheckData.serialize recheckData)

    [<Extension>]
    static member ReportRecheck (property : Property<bool>, recheckData: string, config : IPropertyConfig) : Report =
        property |> Property.falseToFailure |> Property.reportRecheckWith recheckData config

    [<Extension>]
    static member Render (property : Property) : string =
        let (Property property) = property
        Property.render property

    [<Extension>]
    static member Render (property : Property, config : IPropertyConfig) : string =
        let (Property property) = property
        Property.renderWith config property

    [<Extension>]
    static member Render (property : Property<bool>) : string =
        property |> Property.falseToFailure |> Property.render

    [<Extension>]
    static member Render (property : Property<bool>, config : IPropertyConfig) : string =
        property |> Property.falseToFailure |> Property.renderWith config

    [<Extension>]
    static member Where (property : Property<'T>, filter : Func<'T, bool>) : Property<'T> =
        Property.filter filter.Invoke property

    [<Extension>]
    static member Select (property : Property<'T>, mapper : Func<'T, 'TResult>) : Property<'TResult> =
        property
        |> Property.map mapper.Invoke

    [<Extension>]
    static member Select (property : Property<'T>, mapper : Action<'T>) : Property =
        property
        |> Property.map mapper.Invoke
        |> Property

    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, Property<'TCollection>>, projection : Func<'T, 'TCollection, 'TResult>) : Property<'TResult> =
        property |> Property.bind (fun a ->
            binder.Invoke a |> Property.map (fun b -> projection.Invoke (a, b)))

    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, Property<'TCollection>>, projection : Action<'T, 'TCollection>) : Property =
        let result =
            property |> Property.bind (fun a ->
                binder.Invoke a |> Property.map (fun b ->
                    projection.Invoke (a, b)))
        Property result

    // Async support - allow binding Task<'T> in LINQ comprehensions
    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, System.Threading.Tasks.Task<'TCollection>>, projection : Func<'T, 'TCollection, 'TResult>) : Property<'TResult> =
        property |> Property.bind (fun a ->
            Property.ofTask (binder.Invoke a) |> Property.map (fun b -> projection.Invoke (a, b)))

    // Async support - allow binding Task<'T> in LINQ comprehensions with Action projection
    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, System.Threading.Tasks.Task<'TCollection>>, projection : Action<'T, 'TCollection>) : Property =
        let result =
            property |> Property.bind (fun a ->
                Property.ofTask (binder.Invoke a) |> Property.map (fun b ->
                    projection.Invoke (a, b)))
        Property result

    // Allow Select with Task<'T> - for simpler task bindings
    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, System.Threading.Tasks.Task<'TResult>>) : Property<'TResult> =
        property |> Property.bind (fun a -> Property.ofTask (binder.Invoke a))

    // Async support - allow binding Task (non-generic) in LINQ comprehensions
    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, System.Threading.Tasks.Task>, projection : Func<'T, unit, 'TResult>) : Property<'TResult> =
        property |> Property.bind (fun a ->
            Property.ofTaskUnit (binder.Invoke a) |> Property.map (fun b -> projection.Invoke (a, b)))

    // Async support - allow binding Task (non-generic) in LINQ comprehensions with Action projection
    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, System.Threading.Tasks.Task>, projection : Action<'T, unit>) : Property =
        let result =
            property |> Property.bind (fun a ->
                Property.ofTaskUnit (binder.Invoke a) |> Property.map (fun b ->
                    projection.Invoke (a, b)))
        Property result

    // Allow Select with Task (non-generic) - for simpler task bindings
    [<Extension>]
    static member SelectMany (property : Property<'T>, binder : Func<'T, System.Threading.Tasks.Task>) : Property<unit> =
        property |> Property.bind (fun a -> Property.ofTaskUnit (binder.Invoke a))

#endif
