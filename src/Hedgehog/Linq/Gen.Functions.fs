namespace Hedgehog.Linq

open System
open System.Runtime.CompilerServices
open Hedgehog
open Hedgehog.FSharp

[<AbstractClass; Sealed>]
type GenFunctionExtensions private () =

    /// <summary>
    /// Generates a list together with a function that maps each of the distinct
    /// elements in the list to values generated by outGen.
    /// </summary>
    /// <remarks>
    /// Distinct elements in the input list may map to the same output values.
    /// For example, [2; 3; 2] may map to ['A'; 'B'; 'A'] or ['A'; 'A'; 'A'],
    /// but never ['A'; 'B'; 'C']. The generated function throws if called with
    /// values not present in the input list.
    /// </remarks>
    /// <param name="inpGen">Generator for the input list.</param>
    /// <param name="outGen">Generator for the output values.</param>
    /// <returns>A generator that produces a tuple of the input list and a mapping function.</returns>
    [<Extension>]
    static member WithMapTo(inpGen : Gen<ResizeArray<'T>>, outGen : Gen<'TResult>) : Gen<struct (ResizeArray<'T> * Func<'T, 'TResult>)> =
        inpGen
        |> Gen.map List.ofSeq
        |> Gen.withMapTo outGen
        |> Gen.map (fun (inputs, f) -> struct (ResizeArray inputs, Func<'T, 'TResult>(f)))

    /// <summary>
    /// Generates a list together with a function that maps each of the distinct
    /// elements in the list to distinct values generated by outGen.
    /// </summary>
    /// <remarks>
    /// Distinct elements in the input list are guaranteed to map to distinct
    /// output values. For example, [2; 3; 2] may map to ['A'; 'B'; 'A'], but
    /// never ['A'; 'A'; 'A'] or ['A'; 'B'; 'C']. Only use this if the output
    /// space is large enough that the required number of distinct output values
    /// are likely to be generated. The generated function throws if called with
    /// values not present in the input list.
    /// </remarks>
    /// <param name="inpGen">Generator for the input list.</param>
    /// <param name="outGen">Generator for the output values.</param>
    /// <returns>A generator that produces a tuple of the input list and a mapping function.</returns>
    [<Extension>]
    static member WithDistinctMapTo(inpGen : Gen<ResizeArray<'T>>, outGen : Gen<'TResult>) : Gen<struct(ResizeArray<'T> * Func<'T, 'TResult>)> =
        inpGen
        |> Gen.map List.ofSeq
        |> Gen.withDistinctMapTo outGen
        |> Gen.map (fun (inputs, f) -> struct (ResizeArray inputs, Func<'T, 'TResult>(f)))
